// OmegaT plugin
apply plugin: "org.omegat.gradle"
dependencies {
    // FIXME: here is ugly configuration for omegat plugin
    omegat files('buildSrc/OmegaT.jar'),
           fileTree('buildSrc/lib')
}

// Generation of IntelliJ localized resources
apply plugin: 'java'
Closure customJarTask = { taskName, targetName ->
    task(taskName, type: Jar, dependsOn: [jar, translate]) {
        baseName targetName
        def fromDir = ''
        if (taskName.startsWith("plugin")) {
           fromDir = "$projectDir/target/plugins/$targetName"
           destinationDir new File("$buildDir/plugins")
        } else {
           fromDir = "$projectDir/target/$targetName"
           destinationDir new File("$buildDir")
        }
        inputs.dir new File("$fromDir")
        from("$fromDir/") {
            exclude(['messages', 'fileTemplates', 'inspectionDescriptions',
                    'intentionDescriptions', 'postfixTemplates', 'search', 'tips', '.ignoreme'])
        }

        into('messages') {                 from "$fromDir/messages"  }
        into('fileTemplates_ja') {         from "$fromDir/fileTemplates" }
        into('inspectionDescriptions_ja') {from "$fromDir/inspectionDescriptions"}
        into('intentionDescriptions_ja') { from "$fromDir/intentionDescriptions"}
        into('postfixTemplates_ja') {      from "$fromDir/postfixTemplates"}
        into('search_ja') {                from "$fromDir/search"}
        into('tips_ja') {                  from "$fromDir/tips"}
    }
}

tasks.addRule("Pattern: plugin<ID>") { String taskName ->
    if (taskName.startsWith("plugin")) {
        def pluginName = taskName[6].toLowerCase() + taskName[7..taskName.length() - 1]
        customJarTask(taskName, pluginName)
    }
}

tasks.addRule("Pattern: ide<ID>") { String taskName ->
    if (taskName.startsWith("ide")) {
        def toolName = taskName[3].toLowerCase() + taskName[4..taskName.length() - 1]
        customJarTask(taskName, toolName)
    }
}

// Includes specifications for resource artifacts
apply from: 'source/source.gradle'

task wrapper(type: Wrapper) {
  gradleVersion = '2.14.1'
}
