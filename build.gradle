// Release management
plugins {
    id "co.riiid.gradle" version "0.4.2"

}
apply from: "$rootDir/gradle/github.gradle"

// Translation
apply plugin: "org.omegat.gradle"

// FIXME: here is ugly configuration for omegat plugin
dependencies {
    omegat fileTree('buildSrc/lib')
}

// Define projects directories.
// -----------
// 15 - IntelliJ IDEA Version 15.x
// 2016 - IntelliJ IDEA Version 2016.x
// and-143.2915847 - Android Studio
// pycharm-2016 - PyCharm
// -----------
allprojects {
     ext {
        target15Dir = new File(rootDir, 'target/15')
        target16Dir = new File(rootDir, 'target/2016')
        targetAndDir = new File(rootDir, 'target/and-143.2915847')
        targetPyDir = new File(rootDir, 'target/pycharm-2016.1')
    }
}

task cleanRootDirs(type: Delete) {
    delete target15Dir
    delete target16Dir
    delete targetAndDir
    delete targetPyDir
    delete new File(rootDir, "build")
}

task removeUglyFiles(type: Delete) {
    delete new File(rootDir, "build/libs")
}

subprojects {
    apply plugin: 'java'
    task prepareDir << {
        new File(projectDir, "src/main/resources").mkdir()
    }
    task removeDir(type: Delete) {
        delete new File(projectDir, "src")
    }

    task copyDistFiles(type: Copy) {
        from new File(project.buildDir, "libs")
        into new File(rootDir, "build/distribution")
    }

    task processPropertiesFile << {
        sourceSets.each { s ->
            s.resources.srcDirs.each { d ->
                if (d.isDirectory()) {
                    ant.native2ascii(
                            src: "${d}",
                            dest: "${s.output.resourcesDir}",
                            includes: '**/*.properties',
                            encoding: 'UTF-8'
                    )
                }
            }
        }
    }
    processResources.excludes = ['**/*.properties']
    processResources.dependsOn processPropertiesFile
    build.dependsOn copyDistFiles
    copyDistFiles.mustRunAfter jar
    removeUglyFiles.mustRunAfter jar
}

Closure processR = { src, dest ->
    ["search", "messages"].each { type ->
        copy {
            from new File(src, type)
            into new File(dest, type)
        }
    }
    ["fileTemplates", "inspectionDescriptions", "intentionDescriptions",
     "postfixTemplates", "tips"].each { type ->
        copy {
            from new File(src, type)
            into new File(dest, type + "_ja")
        }
    }
}

project(":resources-pycharm") {
    apply plugin: 'java'
    defaultTasks 'jar'
    jar.baseName = 'resources_ja'
    jar.appendix = 'py2016.1'

    task processResourceFiles(dependsOn: [prepareDir, '::translate']) << {
        processR(targetPyDir, sourceSets.main.resources.srcDirs[0])
    }
    processResources.dependsOn processResourceFiles
    processPropertiesFile.dependsOn processResourceFiles
    clean.dependsOn removeDir
}

project(":resources-and") {
    apply plugin: 'java'
    defaultTasks 'jar'
    jar.baseName = 'resources_ja'
    jar.appendix = 'and'

    task processResourceFiles(dependsOn: [prepareDir, '::translate']) << {
        processR(targetAndDir, sourceSets.main.resources.srcDirs[0])
   }
    processResources.dependsOn processResourceFiles
    processPropertiesFile.dependsOn processResourceFiles
    clean.dependsOn removeDir
}

project(":resources-2016") {
    apply plugin: 'java'
    defaultTasks 'jar'
    jar.baseName = 'resources_ja'
    jar.appendix = '2016'

    task processResourceFiles(dependsOn: [prepareDir, '::translate']) << {
        processR(target16Dir, sourceSets.main.resources.srcDirs[0])
    }
    processResources.dependsOn processResourceFiles
    processPropertiesFile.dependsOn processResourceFiles
    clean.dependsOn removeDir
}

project(":resources-15") {
    apply plugin: 'java'
    defaultTasks 'jar'
    jar.baseName = 'resources_ja'
    jar.appendix = '15'

    task processResourceFiles(dependsOn: [prepareDir, '::translate']) << {
        processR(target15Dir, sourceSets.main.resources.srcDirs[0])
    }
    processResources.dependsOn processResourceFiles
    processPropertiesFile.dependsOn processResourceFiles
    clean.dependsOn removeDir
    clean.dependsOn '::cleanRootDirs'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.14.1'
}
